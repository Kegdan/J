    toBin =: >@{:@((<.@%&2@>@{.;(1&-@=&0@>@{.#|~&2@>@{.),>@{:)^:_@;&a:) 

 - toBin 1024

>  1 0 0 0 0 0 0 0 0 0 0
 - toBin 111

>  1 1 0 1 1 1 1
 - toBin 14

>  1 1 1 0
 - toBin 4

>  1 0 0

******************************************************

Колличество счастливых билетов 

    2++/*:(>&2#[)(}.-}:)>:\:(}:<}.)/:~,([+/+/~)i.10

******************************************************

    geron =: %:@(*/)@(-:@(+/)-,&0)
 - geron 8 6 10

>  24

******************************************************

задача Проекта Эйлера 28

    do =: >:@+/@(*&4@*:-*&6@<:)@(+&2)@(#~2&|)@i.
 - do 5

>  101
 - do 1001

>  669171001

******************************************************

задача Проекта Эйлера 1

    do =: +/@(#~=&0@(5&|<.3&|))@i.
 - do 10

>  23
 - do 1000

>  233168

******************************************************

найти числа фибоначи, меньшие заданного

    do =: 3 : '}:@([`(,{:@}:+{:)@.(<&y@{:)^:_)1 1'
 - do 100

>  1 1 2 3 5 8 13 21 34 55 89

******************************************************

    reverse =: {:@((<.@%&10@{.,*&10@{:+10&|@{.)`[@.(=&0@{.)^:_)@,&0
 - reverse 123

>  321

******************************************************
интерпретатор брейнфака

    m =: 30000$0
    index =: 0
    output =: 0$0
    input =: 0$0
    code =: 0$0
    cycles =: 0$0
    codeIndex =: 0
    setCode =: 3 : 'code =: transform y'
    transform =: '><+-.,[]'&i.
    preset =: 3 : 'cycles =: 0$0 [ codeIndex =: 0 [ output =: 0$0 [ m =: 256$0 [ index=:0'
    returnOutput =: 3 : 'output { a.'
    changeValue =: 3 : 'm =: (index&{.,y,(>:index)&}.) m'  
    addCodeIndex =: 3 : 'codeIndex =: (>: codeIndex)'
    setCodeIndex =: 3 : 'codeIndex =: {: cycles'
    deleteCycle =: 3 : 'cycles =: }: cycles'
		 
    func_1 =: 3 : 'index =:256&|@>:index'
    func_2 =: 3 : 'index =:256&|@<:index'
    func_3 =: 3 : 'changeValue@(256&|)@>:@(index&{) m'
    func_4 =: 3 : 'changeValue@(256&|)@<:@(index&{) m'
    func_5 =: 3 : 'output=:output, ((index&{) m)'
    func_6 =: 3 : '(input =: }.input)[ changeValue(256&|@{.input)'
    func_7 =: 3 : 'cycles =: cycles&, codeIndex'
    func_8 =: 3 : '(setCodeIndex`deleteCycle@.(0&=))@(index&{) m'

    doAction =: func_1`func_2`func_3`func_4`func_5`func_6`func_7`func_8@.]  
    doInstuction =: 3 : '(doAction[addCodeIndex)@(codeIndex&{) code'
    ExitCond =: 3 : 'codeIndex = (# code)'
		 
    interpreter =: 3 : '(((+&1 [ doInstuction )`0: @. ExitCond)^:_) 0'
    brainFuck =: returnOutput [ interpreter [ setCode [ preset

 - brainFuck '++++++++++[>+++++++>++++++++++>+++>+<<<<-]>++.>+.+++++++..+++.>++.<<+++++++++++++++.>.+++.------.--------.>+.>.'


>  Hello World!

******************************************************

В заданный непустой текст входят только цифры и буквы. Длина текста не более 12 символов.
Определить, удовлетворяется ли следующее свойство:

Текст состоит из чередования букв и цифр, причем если текст начинается с буквы, то он должен и заканчиваться буквой, если с цифры – цифрой (например: p5g6e4d);
        
    laba =: (+./@(E.&(12$0 1))*.{.={:)@(>&58)@(a.&i.) 
 - laba '1n2jjk4l5'

>  0
 - laba '1n2j3k4l5'

>  1
 - laba '1n23324l5'

>  0
